ORG 0000H
LJMP START
ORG 0100H
START:
MAIN:
	;---DN2CH TEST---
	MOV R4,#7H
	MOV R5,#0CEH

	MOV 3FH,#0FFH
	MOV 3EH,#0FDH
	SETB RS1
	SETB RS0
	LCALL DBN2CHR
	CLR RS0
	CLR RS1

	LJMP $
;-----------------------DN2CH 3FH,3EH中的双字节数转BCD存放在R4-R6-----------------------
DBN2CHR:MOV R4,#0
	MOV R5,#0
	MOV R6,#0
	MOV R7,#16
DBN2LOP:MOV A,3EH		;双字节数移位操作
	RLC A
	MOV 3EH,A
	MOV A,3FH
	RLC A
	MOV 3FH,A
	MOV A,R6
	RLC A
	MOV R6,A
	MOV A,R5
	RLC A
	MOV R5,A
	MOV A,R4
	RLC A
	MOV R4,A
	CJNE R7,#1,DBN2JMO 	;最后一次不作调整
	RET
DBN2JMO:CLR C
	MOV A,R6
	ANL A,#0FH
	SUBB A,#5
	JC DBN2JMA		;当前BCD位值大于4则进行+3调整
	MOV A,R6
	ADD A,#3
	MOV R6,A
DBN2JMA:CLR C
	MOV A,R6
	ANL A,#0F0H
	SUBB A,#50H
	JC DBN2JMB
	MOV A,R6
	ADD A,#30H
	MOV R6,A
DBN2JMB:CLR C
	MOV A,R5
	ANL A,#0FH
	SUBB A,#5
	JC DBN2JMC
	MOV A,R5
	ADD A,#3
	MOV R5,A
DBN2JMC:CLR C
	MOV A,R5
	ANL A,#0F0H
	SUBB A,#50H
	JC DBN2JMD
	MOV A,R5
	ADD A,#30H
	MOV R5,A
DBN2JMD:CLR C
	MOV A,R4
	ANL A,#0FH
	SUBB A,#5
	JC DBN2JME
	MOV A,R4
	ADD A,#3
	MOV R4,A	
DBN2JME:CLR C
	MOV A,R4
	ANL A,#0F0H
	SUBB A,#50H
	JC DBN2JMF
	MOV A,R4
	ADD A,#30H
	MOV R4,A
DBN2JMF:DJNZ R7,DBN2LOP
	RET

	
END
